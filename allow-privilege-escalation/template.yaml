apiVersion: templates.gatekeeper.sh/v1alpha1
kind: ConstraintTemplate
metadata:
  name: k8spspallowprivilegeescalationcontainer
spec:
  crd:
    spec:
      names:
        kind: K8sPSPAllowPrivilegeEscalationContainer
        listKind: K8sPSPAllowPrivilegeEscalationContainerList
        plural: k8spspallowprivilegeescalationcontainer
        singular: k8spspallowprivilegeescalationcontainer
  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package k8spspallowprivilegeescalationcontainer

        violation[{"msg": msg, "details": {}}] {
            c := input_containers[_]
            input_allow_priviledge_escalation_allowed(c)
            msg := sprintf("Privilege escalation container is not allowed: %v", [c.name])
        }

        input_allow_priviledge_escalation_allowed(c) {
            not has_field(c, "securityContext")
        }
        input_allow_priviledge_escalation_allowed(c) {
            not has_field(c.securityContext, "allowPrivilegeEscalation")
        }
        input_allow_priviledge_escalation_allowed(c) {
            c.securityContext.allowPrivilegeEscalation
        }
        input_containers[c] {
            c := input.review.object.spec.containers[_]
        }
        input_containers[c] {
            c := input.review.object.spec.initContainers[_]
        }
        # has_field returns whether an object has a field
        has_field(object, field) = true {
            object[field]
        }